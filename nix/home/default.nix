{
  pkgs,
  lib,
  config,
  username,
  ...
}: let
  # Platform-agnostic paths
  homeDir =
    if pkgs.stdenv.isDarwin
    then "/Users/${username}"
    else "/home/${username}";
  ghqRoot = "${homeDir}/ghq"; # cf. git config --global ghq.root
  dotfilesDir = "${ghqRoot}/github.com/i9wa4/dotfiles";

  # Direct symlink (not via Nix store) - changes reflect immediately
  symlink = config.lib.file.mkOutOfStoreSymlink;
in {
  imports = [
    ./agent-skills.nix
    ./codex.nix
    ./editorconfig.nix
    ./git.nix
    ./vscode.nix
  ];
  home = {
    # User info (username is passed from flake.nix via extraSpecialArgs)
    inherit username;
    homeDirectory = lib.mkForce homeDir;

    # Home Manager state version
    # Set to the version of home-manager at initial installation. Do not change.
    # NOTE: This is NOT the home-manager release version. It controls internal
    # migration behavior. Bumping it can trigger unexpected state changes.
    # cf. https://nix-community.github.io/home-manager/options.xhtml#opt-home.stateVersion
    stateVersion = "24.11";

    # PATH additions (prepended to $PATH)
    # cf. https://nix-community.github.io/home-manager/options.xhtml#opt-home.sessionPath
    sessionPath = [
      "${homeDir}/.local/bin"
      "${dotfilesDir}/bin"
    ];

    # User packages (managed by home-manager)
    # For project-specific tools, use devShell or mise instead
    packages = [
      # Cloud & Infrastructure
      pkgs.awscli2
      pkgs.ssm-session-manager-plugin
      pkgs.databricks-cli
      pkgs.google-cloud-sdk
      pkgs.terraform
      pkgs.tflint
      # Version control (git is managed by programs.git)
      pkgs.gh
      pkgs.ghq
      pkgs.lazygit
      # Search
      pkgs.ripgrep
      pkgs.fd
      pkgs.fzf
      # File viewers
      pkgs.bat
      pkgs.jq
      pkgs.yq-go
      # Terminal
      pkgs.tmux
      # System
      pkgs.htop
      pkgs.tailscale
      pkgs.wget
      pkgs.fastfetch
      pkgs.hyperfine
      # Language runtimes
      pkgs.deno
      pkgs.go
      pkgs.nodejs
      pkgs.python3
      pkgs.rustup
      pkgs.uv
      # Linters & Formatters
      pkgs.alejandra
      pkgs.hadolint
      pkgs.shellcheck
      pkgs.shfmt
      pkgs.statix
      pkgs.stylua
      pkgs.zizmor
      # Nix development
      # NOTE: comma is provided by nix-index-database module (programs.nix-index-database.comma.enable)
      pkgs.nurl
      # CI/CD
      pkgs.act
      pkgs.actionlint
      pkgs.gitleaks
      pkgs.mise
      pkgs.ghalint
      # NOTE: ghatm not in nixos-25.11 stable, use `nix run nixpkgs#ghatm`
      pkgs.pinact
      pkgs.rumdl
      # LSP
      pkgs.efm-langserver
      pkgs.nixd
      pkgs.pyright
      # Editors
      pkgs.neovim
      pkgs.vim
      pkgs.vim-startuptime
      # AI coding agent tools
      pkgs.llm-agents.ccusage
      pkgs.llm-agents.ccusage-codex
      pkgs.llm-agents.claude-code
      pkgs.llm-agents.codex
      # NOTE: GUI applications are managed via Homebrew casks
      # cf. nix/flake-parts/darwin.nix
    ];

    # ==========================================================================
    # Dotfiles (direct symlink via mkOutOfStoreSymlink)
    # Changes reflect immediately without rebuild
    # cf. Makefile MF_LINK_HOME_ROWS and MF_LINK_XDG_ROWS
    # ==========================================================================
    file = {
      # .claude/skills is managed by agent-skills.nix
      # .codex/skills is managed by agent-skills.nix
      # .codex/{AGENTS.md,rules} is managed by codex.nix
      # .codex/config.toml is generated by codex.nix activation script
      # zsh: ~/.zshenv sets ZDOTDIR, so zsh reads ~/.config/zsh/.zshrc
      ".zshenv".source = symlink "${dotfilesDir}/config/zsh/.zshenv";

      # ~/.agents/
      ".agents/AGENTS.md".source = symlink "${dotfilesDir}/config/agents/AGENTS.md";
      ".agents/subagents".source = symlink "${dotfilesDir}/config/agents/subagents";
      ".agents/rules".source = symlink "${dotfilesDir}/config/agents/rules";
      ".agents/scripts".source = symlink "${dotfilesDir}/config/agents/scripts";

      # ~/.claude/
      ".claude/CLAUDE.md".source = symlink "${dotfilesDir}/config/agents/AGENTS.md";
      ".claude/agents".source = symlink "${dotfilesDir}/config/agents/subagents";
      ".claude/rules".source = symlink "${dotfilesDir}/config/agents/rules";
      ".claude/scripts".source = symlink "${dotfilesDir}/config/agents/scripts";
      ".claude/settings.json".source = symlink "${dotfilesDir}/config/claude/settings.json";
    };
  };

  xdg.configFile = {
    "efm-langserver".source = symlink "${dotfilesDir}/config/efm-langserver";
    "lazygit".source = symlink "${dotfilesDir}/config/lazygit";
    "nvim".source = symlink "${dotfilesDir}/config/nvim";
    "tmux-a2a-postman".source = symlink "${dotfilesDir}/config/tmux-a2a-postman";
    "rumdl/rumdl.toml".source = symlink "${dotfilesDir}/.rumdl.toml";
    "skk".source = symlink "${dotfilesDir}/config/skk";
    "tmux".source = symlink "${dotfilesDir}/config/tmux";
    "vde".source = symlink "${dotfilesDir}/config/vde";
    "vim".source = symlink "${dotfilesDir}/config/vim";
    "wezterm".source = symlink "${dotfilesDir}/config/wezterm";
    "zeno".source = symlink "${dotfilesDir}/config/zeno";
    # zsh: ZDOTDIR points here
    "zsh".source = symlink "${dotfilesDir}/config/zsh";
  };

  # Nix settings (user-level, written to ~/.config/nix/nix.conf)
  # nix.package is required by HM when writing nix.conf
  # mkDefault allows nix-darwin integration to forward its own nix.package without conflict
  nix.package = lib.mkDefault pkgs.nix;
  nix.settings.experimental-features = ["nix-command" "flakes"];

  programs = {
    # Let Home Manager manage itself
    home-manager.enable = true;

    # nix-index-database: comma (run uninstalled commands via nix-index)
    nix-index-database.comma.enable = true;

    # direnv (auto-activate devShell when cd into project)
    # Note: zsh hook is handled by zinit turbo mode for faster startup
    direnv = {
      enable = true;
      nix-direnv.enable = true;
      enableZshIntegration = false; # Handled by zinit turbo
    };

    # bash: auto-switch to zsh for environments where zsh is not the login shell
    # (e.g., SSM sessions start with /bin/sh, user types "bash" to bootstrap)
    bash = {
      enable = true;
      initExtra = ''
        # Auto-switch to zsh (SSM sets USER=root, fix it before switching)
        if [ -z "$TMUX" ] && [ -z "$ZSH_VERSION" ] && command -v zsh >/dev/null 2>&1; then
          export USER=$(id -un)
          exec zsh -l
        fi
      '';
    };

    # zsh: disabled - config is in config/zsh/ via ZDOTDIR (home.file.".zshenv")
    # zsh package is installed via ubuntu.nix on Linux, nix-darwin programs.zsh on macOS
    zsh.enable = false;
  };

  # ============================================================================
  # Activation scripts (run on darwin-rebuild switch / home-manager switch)
  # ============================================================================
  home.activation = let
    npm = "${pkgs.nodejs}/bin/npm";
    npmPrefix = "${homeDir}/.local";
    git = "${pkgs.git}/bin/git";
    tpmDir = "${dotfilesDir}/config/tmux/plugins/tpm";
  in {
    # 0. Clean temporary files (node caches for security)
    cleanTemporaryFiles = lib.hm.dag.entryAfter ["writeBoundary"] ''
      echo "Cleaning temporary files..."
      # Node.js caches
      rm -rf "${homeDir}/.npm"
    '';

    # 0. Clone tmux plugin manager (tpm)
    cloneTpm = lib.hm.dag.entryAfter ["writeBoundary"] ''
      export PATH="${pkgs.git}/bin:$PATH"
      if [ ! -d "${tpmDir}" ]; then
        echo "Cloning tmux plugin manager..."
        ${git} clone https://github.com/tmux-plugins/tpm "${tpmDir}"
      fi
    '';

    # 1. Install/update safe-chain first (security scanner for npm)
    setupSafeChain = lib.hm.dag.entryAfter ["writeBoundary"] ''
      mkdir -p ${npmPrefix}
      export PATH="${npmPrefix}/bin:${pkgs.nodejs}/bin:$PATH"
      if ! ${npm} --prefix ${npmPrefix} list -g --depth=0 @aikidosec/safe-chain >/dev/null 2>&1; then
        echo "Installing @aikidosec/safe-chain..."
        ${npm} --prefix ${npmPrefix} install -g @aikidosec/safe-chain
        safe-chain setup
      else
        echo "Updating @aikidosec/safe-chain..."
        ${npm} --prefix ${npmPrefix} update -g @aikidosec/safe-chain
      fi
    '';

    # 2. Install/update npm packages (after safe-chain, so they get scanned)
    installNpmPackages = lib.hm.dag.entryAfter ["setupSafeChain"] ''
      export PATH="${npmPrefix}/bin:${pkgs.nodejs}/bin:$PATH"
      NPM_PACKAGES=(
        "@devcontainers/cli"
        "vde-layout"
        "vde-monitor"
        "vde-worktree"
      )

      # Install missing packages
      for pkg in "''${NPM_PACKAGES[@]}"; do
        if ! ${npm} --prefix ${npmPrefix} list -g --depth=0 "$pkg" >/dev/null 2>&1; then
          echo "Installing $pkg..."
          ${npm} --prefix ${npmPrefix} install -g "$pkg"
        fi
      done

      # Update outdated packages in one batch
      outdated=$(${npm} --prefix ${npmPrefix} outdated -g --parseable --depth=0 2>/dev/null | cut -d: -f4 || true)
      if [ -n "$outdated" ]; then
        echo "Updating outdated packages: $outdated"
        echo "$outdated" | xargs ${npm} --prefix ${npmPrefix} install -g
      fi

      # Remove unlisted packages (keep npm, corepack, safe-chain)
      installed=$(${npm} --prefix ${npmPrefix} list -g --depth=0 --parseable 2>/dev/null | tail -n +2 | xargs -I{} basename {} || true)
      for pkg in $installed; do
        case "$pkg" in
          npm|corepack|@aikidosec/safe-chain) continue ;;
        esac
        found=0
        for want in "''${NPM_PACKAGES[@]}"; do
          if [ "$pkg" = "$want" ]; then found=1; break; fi
        done
        if [ "$found" = "0" ]; then
          echo "Removing unlisted package: $pkg"
          ${npm} --prefix ${npmPrefix} uninstall -g "$pkg"
        fi
      done
    '';
  };
}
