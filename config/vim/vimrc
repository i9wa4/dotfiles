" ~/.config/nvim/init.vim
set encoding=utf-8

filetype off
filetype plugin indent off
syntax off


" --------------------------------------
" Variable
"
let g:markdown_recommended_style = 0
let g:netrw_banner = 0
let g:netrw_dirhistmax = 0
let g:netrw_hide = 0
let g:netrw_liststyle = 0
let g:vim_indent = #{
\   line_continuation: 0
\ }

let g:mnh_header_level_shift = 7
let g:my_aa_path = '~/ghq/github.com/i9wa4/dotfiles/config/agents/AGENTS.md'->expand()
let g:my_i0_path = '~/ghq/github.com/i9wa4/internal/work/00/index.qmd'->expand()
let g:my_i1_path = '~/ghq/github.com/i9wa4/internal/work/05/index.qmd'->expand()
let g:my_i2_path = '~/ghq/github.com/i9wa4/internal/work/07/index.qmd'->expand()
let g:python3_host_prog = exepath('python3')
let g:denops#debug = 1


" --------------------------------------
" Keymap
"
nnoremap gf gF

" https://zenn.dev/mattn/articles/83c2d4c7645faa
nmap <SID>g <Nop>
nmap gj gj<SID>g
nmap gk gk<SID>g
nnoremap <script> <SID>gj gj<SID>g
nnoremap <script> <SID>gk gk<SID>g

" https://blog.atusy.net/2025/08/08/map-minus-to-blackhole-register/
nnoremap - "_
xnoremap - "_

" Toggle Quote
nnoremap gqq :call utils#toggle_quote()<CR>
vnoremap gqq :call utils#toggle_quote()<CR>

" Insert Mode
" i_CTRL-T Insert one indent
" i_CTRL-D Delete one indent
inoremap ,now <C-r>=strftime('%Y-%m-%d %X +0900')<CR>
inoremap ,today <C-r>=strftime('%Y-%m-%d')<CR>

" Location List
command! Lprevious  try | lprevious | catch | llast   | catch | endtry
command! Lnext      try | lnext     | catch | lfirst  | catch | endtry
nnoremap <C-p> :Lprevious<CR>
nnoremap <C-n> :Lnext<CR>

" Resize Window
" https://zenn.dev/mattn/articles/83c2d4c7645faa
nmap <C-w>H <C-w><<SID>ws
nmap <C-w>J <C-w>-<SID>ws
nmap <C-w>K <C-w>+<SID>ws
nmap <C-w>L <C-w>><SID>ws
nmap <SID>ws <Nop>
nnoremap <script> <SID>wsH <C-w><<SID>ws
nnoremap <script> <SID>wsJ <C-w>-<SID>ws
nnoremap <script> <SID>wsK <C-w>+<SID>ws
nnoremap <script> <SID>wsL <C-w>><SID>ws

" https://lambdalisue.hatenablog.com/entry/2015/12/25/000046
" Edit
nnoremap <Plug>(my-Edit) <Nop>
nmap <Space>e <Plug>(my-Edit)
nnoremap <Plug>(my-Edit)r  <Cmd>%s/\r$//e<CR>
nnoremap <Plug>(my-Edit)s  <Cmd>%s/\s\+$//e<CR>
nnoremap <Plug>(my-Edit)n  <Cmd>%s/\%ua0//e<CR>
nnoremap <Plug>(my-Edit)qa <Cmd>%s/\([^,]*\)/"\1"/g<CR>
nnoremap <Plug>(my-Edit)qr <Cmd>%s/"//g<CR>

" Filer
" nnoremap <Plug>(my-Filer)cd <Cmd>execute 'cd' fnamemodify(finddir('.git', escape(expand(getcwd()), ' ') .. ';', 1), ':h')<CR>
nnoremap <Plug>(my-Filer) <Nop>
nmap <Space>f <Plug>(my-Filer)
nnoremap <Plug>(my-Filer)aa <Cmd>execute 'edit' g:my_aa_path<CR>
nnoremap <Plug>(my-Filer)c  <Cmd>execute '15Lexplore'<CR>
nnoremap <Plug>(my-Filer)i0 <Cmd>execute 'edit' g:my_i0_path<CR>
nnoremap <Plug>(my-Filer)i1 <Cmd>execute 'edit' g:my_i1_path<CR>
nnoremap <Plug>(my-Filer)i2 <Cmd>execute 'edit' g:my_i2_path<CR>
nnoremap <Plug>(my-Filer)o  <Cmd>execute '15Lexplore' '%:p:h'->expand()<CR>
nnoremap <Plug>(my-Filer)t  <Cmd>execute 'edit' g:my_tp_path<CR>
nnoremap <Plug>(my-Filer)v  <Cmd>execute 'edit' $MYVIMRC<CR>

" Switch
nnoremap <Plug>(my-Switch) <Nop>
nmap <Space>s <Plug>(my-Switch)
nnoremap <Plug>(my-Switch)b <Cmd>setlocal scrollbind! scrollbind?<CR>
nnoremap <Plug>(my-Switch)l <Cmd>setlocal list! list?<CR>
nnoremap <Plug>(my-Switch)n <Cmd>setlocal number! number?<CR>
nnoremap <Plug>(my-Switch)p <Cmd>setlocal paste! paste?<CR>
nnoremap <Plug>(my-Switch)r <Cmd>setlocal relativenumber! relativenumber?<CR>
nnoremap <Plug>(my-Switch)s <Cmd>setlocal spell! spell?<CR>
nnoremap <Plug>(my-Switch)t <Cmd>setlocal expandtab! expandtab?<CR>
nnoremap <Plug>(my-Switch)w <Cmd>setlocal wrap! wrap?<CR>


" --------------------------------------
" Command
"
command! -nargs=? C call setreg('+', getreg(<q-args>))

command! CreateLocalVimrc
\ call system('echo "let g:mnh_header_level_shift = 1" > ./local.vim')


" --------------------------------------
" Autocmd
"
augroup MyAutocmd
  autocmd!

  autocmd BufEnter * checktime
  autocmd BufReadPost * silent! normal! g`"
  autocmd BufEnter,BufNewFile *
  \ call utils#source_local_vimrc('<afile>:p'->expand())

  autocmd FileType *
  \   setlocal spelllang+=cjk spell
  \|  if &diff | setlocal nospell | endif
  autocmd BufNewFile,BufReadPost *.tf,*.tftpl
  \ setfiletype terraform

  autocmd BufEnter,WinEnter,ColorScheme *
  \ call utils#highlight()
augroup END

let g:auto_reload = timer_start(
\   1000,
\   {-> execute('silent! checktime')},
\   {'repeat': -1}
\ )


" --------------------------------------
" Option
"
" Edit
set completeopt=menuone,noinsert,noselect
set fileencodings=utf-8,euc-jp,cp932
set fileformats=unix,dos,mac
set hidden
set nofoldenable autoindent
set nrformats=unsigned
set virtualedit=block

" Search
if executable('rg')
  let &grepprg = 'rg --vimgrep --no-heading'
  set grepformat=%f:%l:%c:%m
endif
set hlsearch
set ignorecase
set incsearch
set shortmess+=c
set shortmess-=S
set smartcase
set wrapscan

" View
set ambiwidth=double
" space:\\u2423,extends:\\u00BB,precedes:\\u00AB
set list listchars=space:␣,tab:>-,trail:~,nbsp:%,extends:»,precedes:«
set number
set signcolumn=number

" Window
set noequalalways
set splitbelow
set splitright
set wildmenu wildoptions=pum,tagfile wildchar=<Tab>

" CommandLine
set cmdheight=1
set showcmd

" StatusLine
set laststatus=2

" TabLine
set showtabline=2

" Highlight
set cursorline

" Abbreviation
" :w' --> :w
cnoreabbrev w' w


" --------------------------------------
" Minimum setting
"
" Variable
let g:netrw_home = $XDG_CACHE_HOME->expand()

" System
set autoread
set belloff=all
set nobackup
set noswapfile
set noundofile
set clipboard&

if has('mac')
  set clipboard^=unnamed
else
  set clipboard^=unnamedplus
endif

if has('mac')
  if !has('nvim') && exists('v:clipproviders')
    " Vim 9.1+ on macOS
    function! s:PBAvailable() abort
      return '+*'
    endfunction
    function! s:PBPaste(reg, opts) abort
      return [systemlist('pbpaste'), 'v']
    endfunction
    function! s:PBCopy(reg, regtype, lines) abort
      call system('pbcopy', join(a:lines, "\n") .. (a:regtype ==# 'V' ? "\n" : ''))
    endfunction
    let v:clipproviders.pb = {
    \   'available': function('s:PBAvailable'),
    \   'paste': {'+': function('s:PBPaste'), '*': function('s:PBPaste')},
    \   'copy':  {'+': function('s:PBCopy'),  '*': function('s:PBCopy')},
    \ }
    set clipmethod=pb,gui,x11,wayland
  else
    " Neovim on macOS
    let g:clipboard = {
    \   'name': 'pbcopy',
    \   'copy':  {'+': ['pbcopy'], '*': ['pbcopy']},
    \   'paste': {'+': ['pbpaste'], '*': ['pbpaste']},
    \   'cache_enabled': 0,
    \ }
  endif
else
  " Linux: Neovim auto-detects, Vim needs explicit provider
  if !has('nvim')
    if executable('xclip')
      let g:clipboard = {
      \   'name': 'xclip',
      \   'copy':  {'+': ['xclip', '-selection', 'clipboard'], '*': ['xclip', '-selection', 'primary']},
      \   'paste': {'+': ['xclip', '-selection', 'clipboard', '-o'], '*': ['xclip', '-selection', 'primary', '-o']},
      \   'cache_enabled': 0,
      \ }
    elseif executable('xsel')
      let g:clipboard = {
      \   'name': 'xsel',
      \   'copy':  {'+': ['xsel', '--clipboard', '--input'], '*': ['xsel', '--primary', '--input']},
      \   'paste': {'+': ['xsel', '--clipboard', '--output'], '*': ['xsel', '--primary', '--output']},
      \   'cache_enabled': 0,
      \ }
    elseif executable('wl-copy')
      let g:clipboard = {
      \   'name': 'wl-clipboard',
      \   'copy':  {'+': ['wl-copy'], '*': ['wl-copy', '--primary']},
      \   'paste': {'+': ['wl-paste'], '*': ['wl-paste', '--primary']},
      \   'cache_enabled': 0,
      \ }
    endif
  endif
endif

" Plugin
if !has('nvim')
  packadd! editorconfig
endif


" --------------------------------------
" dpp.vim
"
let s:dpp_path = $XDG_CONFIG_HOME->expand() .. '/vim/dpp/dpp.vim'
if filereadable(s:dpp_path) && !exists('*dpp#min#load_state')
  execute 'source' s:dpp_path
endif


" --------------------------------------
" End of settings
"
set background=dark

filetype plugin on
syntax enable

if has('termguicolors')
  set termguicolors
endif

execute 'colorscheme' get(g:, 'colors_name', 'retrobox')
